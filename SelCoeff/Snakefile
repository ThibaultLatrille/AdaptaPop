import os
import pandas as pd
from glob import glob

configfile: 'config.yaml'

ROOT = os.path.abspath('..')
FOLDER = os.path.abspath('.')
ANALYSIS_FOLDER = FOLDER + "/analysis_" + config["SAMPLE_LIST"].replace(".tsv", "")
SFS_LIST = []
POPU_TO_TSV = {}

for id, row in list(pd.read_csv(config["SAMPLE_LIST"],sep='\t').iterrows()):
    for popu_path in glob(ROOT + '/Polymorphism/{0}/CDS.ANNOT.*.{1}.tsv.gz'.format(row["Species"],row["SampleName"])):
        POPU_TO_TSV[row["SampleName"]] = popu_path
        SFS_LIST.append(FOLDER + f"/SFS/{row['Species']}.{row['SampleName']}.pdf")

localrules: all, selcoeff, sfs

rule all:
    input: FOLDER + "/sfs-list-doc.pdf"

rule selcoeff:
    input:
        vcf=lambda wildcards: POPU_TO_TSV[wildcards.popu].replace(".tsv.gz", ".vcf.gz"),
        tsv=lambda wildcards: POPU_TO_TSV[wildcards.popu],
        script=ROOT + '/scripts/vcf_annotate_selcoeffs.py',
        exp_folder=ROOT + '/OrthoMam/Experiments/'
    output:
        vcf=FOLDER + "/VCF/{species}.{popu}.vcf.gz"
    shell:
        'python3 {input.script} --vcf {input.vcf} --tsv {input.tsv} --folder {input.exp_folder} --output {output.vcf}'

rule sfs:
    input:
        vcf=rules.selcoeff.output.vcf,
        script=ROOT + '/scripts/plot_sfs.py',
    output:
        pdf=FOLDER + "/SFS/{species}.{popu}.pdf"
    shell:
        'python3 {input.script} --vcf {input.vcf} --output {output.pdf}'

rule latex:
    input:
        sfs=SFS_LIST,
        tex=FOLDER + "/sfs-list-doc.tex",
        script=ROOT + '/scripts/tex_sfs.py'
    output:
        tex=FOLDER + "/sfs-list-include.tex",
        pdf=FOLDER + "/sfs-list-doc.pdf"
    shell: 'python3 {input.script} --sfs {input.sfs} --tex_doc {input.tex} --tex_include {output.tex}'
