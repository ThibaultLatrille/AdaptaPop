import os
import pandas as pd
from glob import glob
from snakemake.io import expand, touch

configfile: 'config.yaml'

ROOT = os.path.abspath('..')
FOLDER = os.path.abspath('.')
GRANULARITY = config['GRANULARITY']
SUBSAMPLING_SITES = config['SUBSAMPLING_SITES']
SUBSAMPLING_GENES = config['SUBSAMPLING_GENES']

NBR_BATCHES = config['NBR_BATCHES']
NBR_REPLICATES_PER_BATCH = config['NBR_REPLICATES_PER_BATCH']
SUBSAMPLING_WEIGHTED = config['SUBSAMPLING_WEIGHTED']

DFE_DICT = {"dfem": ROOT + '/utils/dfem/dfem',
            "grapes": ROOT + '/utils/grapes/grapes/grapes',
            "polyDFE": ROOT + '/utils/polyDFE/polyDFE-2.0-linux-64-bit'}
DFE_MODELS = [DFE_DICT[m] for m in config['DFE_MODELS'] if m in DFE_DICT]

BATCHES = range(1,NBR_BATCHES + 1)

EXP_LIST = []
SPECIES = {}
SUBSAMPlE_SFS = {}
for id, row in list(pd.read_csv(config["SAMPLE_LIST"],sep='\t').iterrows()):
    SPECIES[row["Species"]] = row["SisterSpecies"]
    SPECIES[row["SampleName"]] = row["k"]
    for popu_path in glob(ROOT + '/Polymorphism/{0}/CDS.ANNOT.*.{1}.tsv.gz'.format(row["Species"],row["SampleName"])):
        for granularity in GRANULARITY:
            dir = "{0}-{1}-{2}".format(row["Species"],row["SampleName"],granularity)
            for model in config['DFE_MODELS']:
                for sfs in config['SFS']:
                    if (model == "polyDFE" or model == "MK" or model == "aMK") and sfs == "folded": continue
                    if (model == "polyDFE" or model == "grapes") and (row["SampleName"] == "MOOA"): continue
                    if NBR_BATCHES > 0:
                        EXP_LIST.append(dir + "/histogram-{0}-{1}.tsv".format(model,sfs))

localrules: all,pickle,parse_results,parse_results_gathered,plot_heatmap,plot_violin,plot_scatter,table_ontology

rule all:
    input:
        "Analysis/results.tex",
        "Analysis/violin_plot",
        expand("scatterplot_{granularity}.pdf",granularity=GRANULARITY),
        "ontology/gene_adaptive_table.pdf","ontology/gene_epistasis_table.pdf",
        "ontology/site_adaptive_table.pdf","ontology/site_epistasis_table.pdf","ontology/site_strongly-adaptive_table.pdf"

rule pickle:
    input:
        ali_folder=ROOT + '/OrthoMam/Datasets/omm_NT_fasta.v10c_116',
        div_folder=ROOT + '/OrthoMam/Experiments',
        xml=ROOT + '/OrthoMam/Datasets/omm_markers',
        script=ROOT + '/scripts/annotation_ensg.py'
    output:
        tsv=FOLDER + "/pickle.{species}.{granularity}.pk1.bz2"
    params:
        species=lambda wildcards: "--focal_species {0} --sister_species {1}".format(wildcards.species,
            SPECIES[wildcards.species])
    shell:
        'python3 {input.script} {params.species} --granularity {wildcards.granularity} --ali_folder {input.ali_folder} --div_folder {input.div_folder} --xml {input.xml} --output {output.tsv}'

rule generate_DoFE:
    input:
        vfc=lambda wildcards: glob(ROOT + '/Polymorphism/{0}/CDS.ANNOT.*.{1}.tsv.gz'.format(wildcards.species,wildcards.popu)),
        pickle=lambda wildcards: FOLDER + "/pickle.{0}.{1}.pk1.bz2".format(wildcards.species,wildcards.granularity),
    params:
        dfe=f"--dfe_path {DFE_MODELS}" if len(DFE_MODELS) > 0 else "",
        script=ROOT + '/scripts/generate_DoFE.py',
        time="2-00:00",mem=3000,threads=1,queue="normal",name=lambda wildcards: "{0}-{1}-{2}-{3}".format(wildcards.species,wildcards.popu,wildcards.granularity,wildcards.batch),
        tmp_folder=lambda wildcards: FOLDER + "/{0}-{1}-{2}/tmp/{3}/".format(wildcards.species,wildcards.popu,wildcards.granularity,wildcards.batch),
        output=lambda wildcards: FOLDER + "/{0}-{1}-{2}/{3}/".format(wildcards.species,wildcards.popu,wildcards.granularity,wildcards.sfs),
        species=lambda wildcards: "--focal_species {0} --sister_species {1} --sfs {2} --subsample {3}".format(wildcards.species,
            SPECIES[wildcards.species],wildcards.sfs,SPECIES[wildcards.popu])
    output:
        touch("{species}-{popu}-{granularity}/{sfs}/{batch}.txt")
    shell:
        'mkdir -p {params.tmp_folder} && mkdir -p {params.output} && python3 {params.script} --pickle {input.pickle} {params.dfe} --seed {wildcards.batch} --vcf {input.vfc} --granularity {wildcards.granularity} --rep {NBR_REPLICATES_PER_BATCH} --nbr_sites {SUBSAMPLING_SITES} --nbr_genes {SUBSAMPLING_GENES} --weighted {SUBSAMPLING_WEIGHTED} --output {params.output}{wildcards.batch} --tmp_folder {params.tmp_folder} {params.species}'

rule parse_results:
    input:
        dfe_res=expand("{{species}}-{{popu}}-{{granularity}}/{{sfs}}/{batch}.txt",batch=BATCHES),
    params:
        script=ROOT + '/scripts/parse_results.py',
        folder=lambda wildcards: "--folder {0}-{1}-{2}/{3}".format(wildcards.species,wildcards.popu,wildcards.granularity,wildcards.sfs)
    output:
        plot="{species}-{popu}-{granularity}/histogram-{model}-{sfs}.tsv"
    shell:
        'python3 {params.script} {params.folder} --model {wildcards.model} --output {output.plot}'

rule parse_results_gathered:
    input:
        tsv=EXP_LIST,
        script=ROOT + '/scripts/parse_results_gathered.py'
    output:
        tsv="Analysis/results.tsv"
    shell:
        'mkdir -p Analysis && python3 {input.script} --tsv {input.tsv} --output Analysis'

rule plot_violin:
    input:
        tsv=EXP_LIST,
        script=ROOT + '/scripts/plot_violin.py'
    output:
        tsv=touch("Analysis/violin_plot")
    shell:
        'mkdir -p Analysis/ViolinPlot && python3 {input.script} --tsv {input.tsv} --output Analysis/ViolinPlot'


rule plot_heatmap:
    input:
        sample_list=FOLDER + "/" + config["SAMPLE_LIST"],
        violin="Analysis/violin_plot",
        tsv="Analysis/results.tsv",
        script=ROOT + '/scripts/plot_heatmap.py'
    output: "Analysis/results.tex"
    shell: 'python3 {input.script} --tsv {input.tsv} --sample_list {input.sample_list} --output {output}'

rule plot_scatter:
    input:
        folder=ROOT + '/OrthoMam/Experiments',
        script=ROOT + '/scripts/plot_scatter.py'
    output:
        plot="scatterplot_{granularity}.pdf"
    shell:
        'python3 {input.script} --folder {input.folder} --granularity {wildcards.granularity} --output {output.plot}'

rule table_ontology:
    input:
        folder=ROOT + '/OrthoMam/Experiments',
        xml=ROOT + '/OrthoMam/Datasets/omm_markers',
        script=ROOT + '/scripts/table_ontology.py'
    output:
        tex="ontology/{granularity}_{category}_table.tex",
        pdf="ontology/{granularity}_{category}_table.pdf"
    shell:
        'python3 {input.script} --folder {input.folder} --xml {input.xml} --granularity {wildcards.granularity} --category {wildcards.category} --output {output.tex}'
